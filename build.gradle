plugins {
  id 'maven-publish'
  id 'signing'
}

allprojects {
  apply plugin: 'java-library'

  repositories {
    mavenCentral()
  }

  dependencies {
    api 'org.ow2.asm:asm:9.7'
    api 'org.ow2.asm:asm-tree:9.7'
    api 'org.ow2.asm:asm-util:9.7'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.0'
  }

  compileJava {
    options.compilerArgs += [
      '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED'
    ]
  }

  java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withSourcesJar()
    withJavadocJar()
  }

  test {
    // Test without attach mechanism and dynamic agent loading
    var arguments = [
      '-XX:+DisableAttachMechanism',
      '-XX:-EnableDynamicAgentLoading'
    ]

    // TODO: find a way to avoid this check on latest OpenJ9
    if (System.getProperty("java.vm.vendor") == "Eclipse OpenJ9") {
      arguments.remove('-XX:-EnableDynamicAgentLoading')
    }

    jvmArgs(arguments)

    useJUnitPlatform()
  }
}

group = "io.github.usernugget"
version = "2.0.0"

javadoc {
  options.addStringOption('-add-exports', 'java.base/jdk.internal.misc=ALL-UNNAMED')
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      repositories {
        maven {
          url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
          credentials {
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
          }
        }
      }

      pom {
        name = 'class-redefiner'
        description = "Library to modify classes at runtime"
        url = 'https://github.com/UserNugget/class-redefiner/'

        developers {
          developer {
            id = 'usernugget'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/UserNugget/class-redefiner/'
          developerConnection = 'scm:git:ssh://github.com:UserNugget/class-redefiner.git'
          url = 'https://github.com/UserNugget/class-redefiner/tree/main/'
        }

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
      }
    }
  }
}

signing {
  useInMemoryPgpKeys(
    System.getenv("PGP_SIGNING_KEY"),
    System.getenv("PGP_SIGNING_KEY_PASSPHRASE")
  )

  sign publishing.publications.mavenJava
}