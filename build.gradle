plugins {
  id 'maven-publish'
  id 'signing'
}

allprojects {
  apply plugin: 'java-library'

  repositories {
    mavenCentral()
  }

  dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
  }

  java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withSourcesJar()
    withJavadocJar()
  }

  test {
    useJUnitPlatform()
  }
}

dependencies {
  implementation 'org.ow2.asm:asm:9.5'
  implementation 'org.ow2.asm:asm-tree:9.5'
  implementation 'org.ow2.asm:asm-util:9.5'
  implementation 'org.ow2.asm:asm-commons:9.5'

  int classVersion = (int) Double.parseDouble(System.getProperty("java.class.version"));
  if (classVersion < 53) {
    compileOnly files("${System.getProperty("java.home")}/../lib/tools.jar")
  }
}

group = "io.github.usernugget"
version = "1.1.0"

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      repositories {
        maven {
          url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
          credentials {
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
          }
        }
      }

      pom {
        name = 'class-redefiner'
        description = "Library to modify classes at runtime"
        url = 'https://github.com/UserNugget/class-redefiner/'

        developers {
          developer {
            id = 'usernugget'
          }
        }

        scm {
          connection = 'scm:git:git://github.com/UserNugget/class-redefiner/'
          developerConnection = 'scm:git:ssh://github.com:UserNugget/class-redefiner.git'
          url = 'https://github.com/UserNugget/class-redefiner/tree/main/'
        }

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
      }
    }
  }
}

signing {
  useInMemoryPgpKeys(
    System.getenv("PGP_SIGNING_KEY"),
    System.getenv("PGP_SIGNING_KEY_PASSPHRASE")
  )

  sign publishing.publications.mavenJava
}